resource "aws_s3_bucket" "website" {
  bucket = var.bucket_name

  tags = merge(local.default_tags, {
    Name = "${local.resource_prefix}-bucket"
  })
}

# SSE-KMS encryption on the website bucket (required by Trivy/security scan).
# The key policy grants CloudFront decrypt access so it can serve objects
# directly from S3 via OAC without an intermediate Lambda@Edge.
resource "aws_kms_key" "website_bucket" {
  description             = "KMS key for ${local.resource_prefix} S3 bucket encryption"
  deletion_window_in_days = 10
  enable_key_rotation     = true

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid    = "EnableRootAccountAccess"
        Effect = "Allow"
        Principal = {
          AWS = "arn:aws:iam::${data.aws_caller_identity.current.account_id}:root"
        }
        Action   = "kms:*"
        Resource = "*"
      },
      {
        Sid    = "AllowCloudFrontDecrypt"
        Effect = "Allow"
        Principal = {
          Service = "cloudfront.amazonaws.com"
        }
        Action = [
          "kms:Decrypt",
          "kms:GenerateDataKey*"
        ]
        Resource = "*"
        Condition = {
          StringEquals = {
            "AWS:SourceArn" = aws_cloudfront_distribution.website.arn
          }
        }
      }
    ]
  })

  tags = merge(local.default_tags, {
    Name = "${local.resource_prefix}-bucket-key"
  })
}

resource "aws_kms_alias" "website_bucket" {
  name          = "alias/${local.resource_prefix}-bucket"
  target_key_id = aws_kms_key.website_bucket.key_id
}

resource "aws_s3_bucket_server_side_encryption_configuration" "website" {
  bucket = aws_s3_bucket.website.id

  rule {
    apply_server_side_encryption_by_default {
      sse_algorithm     = "aws:kms"
      kms_master_key_id = aws_kms_key.website_bucket.arn
    }
  }
}

resource "aws_s3_bucket_public_access_block" "website" {
  bucket = aws_s3_bucket.website.id

  block_public_acls       = true
  block_public_policy     = true
  ignore_public_acls      = true
  restrict_public_buckets = true
}

resource "aws_s3_bucket_ownership_controls" "website" {
  bucket = aws_s3_bucket.website.id

  rule {
    object_ownership = "BucketOwnerPreferred"
  }
}

resource "aws_s3_object" "website_files" {
  for_each = local.site_files

  bucket        = aws_s3_bucket.website.id
  key           = each.key
  source        = "${var.site_directory_path}/${each.key}"
  source_hash   = filemd5("${var.site_directory_path}/${each.key}")
  content_type  = local.mime_types[regex("\\.[^.]+$", each.key)]
  cache_control = each.key == "index.html" ? "no-cache" : "public, max-age=31536000, immutable"

  tags = merge(local.default_tags, {
    Name = "${local.resource_prefix}-${replace(each.key, "/", "-")}"
  })
}

resource "aws_s3_bucket_policy" "website" {
  bucket = aws_s3_bucket.website.id

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid    = "AllowCloudFrontServicePrincipal"
        Effect = "Allow"
        Principal = {
          Service = "cloudfront.amazonaws.com"
        }
        Action   = "s3:GetObject"
        Resource = "${aws_s3_bucket.website.arn}/*"
        Condition = {
          StringEquals = {
            "AWS:SourceArn" = aws_cloudfront_distribution.website.arn
          }
        }
      }
    ]
  })
}

resource "aws_s3_object" "config" {
  bucket        = aws_s3_bucket.website.id
  key           = "config.js"
  content_type  = "application/javascript"
  cache_control = "no-cache"

  content = <<-EOT
// Auto-generated by Terraform - Do not edit manually
window.__APP_CONFIG__ = Object.assign(window.__APP_CONFIG__ || {}, ${jsonencode(var.runtime_config)});
EOT

  tags = merge(local.default_tags, {
    Name = "${local.resource_prefix}-config"
  })
}
